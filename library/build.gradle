apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    flavorDimensions "dimension"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 4
        versionName "1.2.0"
        setProperty("archivesBaseName", "android-material-chips-$versionName")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.rengwuxian.materialedittext:library:2.1.4'

    // JUnit
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'junit:junit:4.12'
    // Testing
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}